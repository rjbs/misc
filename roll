#!/usr/bin/perl
use strict;
use warnings;
use Games::Dice qw(roll_array);
use Getopt::Long::Descriptive;

my ($opt, $usage) = describe_options(
  '%c %o <dice> <diff>',
  [ 'no-botches|B',  'ones count as failures, not botches'  ],
  [ 'reroll-tens|t', 'reroll tens, counting only successes' ],
);

my $dice = shift @ARGV;
my $diff = @ARGV ? shift @ARGV : 6;

die $usage->text unless $dice and $diff;

my @roll = roll_array($dice . "d10");

my $succ = 0;

for my $die (@roll) {
  $succ++ if $die >= $diff;
  $succ-- if $die == 1 and not $opt->{no_botches};

  if ($die == 10 and $opt->{reroll_tens}) {
    my ($die) = roll_array '1d10';
    if ($die >= $diff) {
      push @roll, sprintf "10.%02u", $die;
      $succ++;
    }
  }
}

@roll = sort { $a <=> $b } @roll;
print "result: @roll ($succ successes)\n";
