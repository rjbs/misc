#!/usr/bin/perl

use strict;
use warnings;

use Mac::Glue qw(:glue);

my $ical = Mac::Glue->new("iCal");
my $ab   = Mac::Glue->new("Address Book");

sub birthday_calendar {
	my $cal_name = shift;

	my $bdcal = $ical->obj(calendar => whose(title => equals => $cal_name));

	if ($bdcal) {
		$ical->delete($bdcal->obj('events'));
	} else {
		$bdcal = $ical->make(
			new => 'calendar',
			with_properties => { title => $cal_name }
		);
	}
	$bdcal or die "could not acquire calendar!";
}

sub birthday_people {
	my @people  = $ab->obj('people')->get;
	die "nobody found!" unless @people;
	@people;
}

my $bdcal  = birthday_calendar('Birthdays');

for my $person (birthday_people) {
	my $birthday = $person->prop("birth date")->get;
	next if $birthday eq 'msng';

	my $recur =
		"FREQ=YEARLY;INTERVAL=1;BYMONTH=" .
		((localtime $birthday)[4]+1)      .
		";BYMONTHDAY="                    .
		(localtime $birthday)[3];
	
	$ical->make(
		new => 'event',
		at  => location(end => $bdcal),
		with_properties => {
			summary        => ($person->prop("name")->get . "'s Birthday"),
			'start date'   => $birthday,
			'allday event' => 1,
			recurrence     => $recur
		}
	);
}

print "Calendar updated!\n";
