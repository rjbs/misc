#!/usr/bin/perl
use strict; use warnings;
use Mac::PropertyList;
use Netscape::Bookmarks;

my $file  = "$ENV{HOME}/Library/Preferences/com.omnigroup.OmniWeb5.plist";
my $prefs = Mac::PropertyList::parse_plist_file($file);
die "can't parse $file" unless $prefs;

my $shortcuts = $prefs->value('OW5AddressShortcuts');

my %links;
foreach ($shortcuts->keys) {
	my $takes_param = ((my $keyword = $_) =~ s/\@$//);
	$keyword .= '.' if ($shortcuts->value($_.'@') and not $takes_param);

	$links{$keyword} = {
		title => $shortcuts->value($_)->value('name'),
		href  => $shortcuts->value($_)->value('format'),
	};

	$links{$keyword}{href} =~ s/%@/%s/g;
}

my $bookmarks = Netscape::Bookmarks->new;
my $root = Netscape::Bookmarks::Category->new({ title => 'OmniWeb Shortcuts' });
$bookmarks->add(\$root); # I shouldn't need to pass a ref!

for (keys %links) {
	my $link = Netscape::Bookmarks::Link->new({
		TITLE => $links{$_}{title},
		HREF  => $links{$_}{href},
		SHORTCUTURL => $_, # crucial; requires patched NS::B
	});
	$root->add(\$link); # I shouldn't need to pass a ref!
}

print $bookmarks->as_string;
