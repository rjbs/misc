use Email::MIME;
use Email::MIME::Creator;
use Email::Send;
use Template;

my $dir  = shift or die "usage: send.pl [source_dir]";
my $tt2  = Template->new;
my $from = 'Email Sender <sender@example.com>';
my $subj = 'This is a Form Letter!';

open my $addr_file, '<', "$dir/addrs" or die "can't open address file: $!";

for ( map { chomp; [ split /\t/ ] } <$addr_file> ) {
	print "sending mail for $_->[1]\n";
	send SMTP => message_for(@$_)->as_string => $ENV{SMTPSERVER};
}

sub message_for {
	my (%recipient, @parts);
	@recipient{qw(greeting to)} = @_;
	
	for (grep { -r "$dir/part.$_" } qw(plain html)) {
		$tt2->process("$dir/part.$_", \%recipient, \(my $body));
		push @parts, Email::MIME->create(
			attributes => { content_type => "text/$_", },
			body       => $body
		);
	}

	Email::MIME->create(
		header     => [ To => $recipient{to}, From => $from, Subject => $subj, ],
		attributes => { content_type => 'multipart/alternative' },
		parts      => [ @parts ],
	);
}
