#!/usr/bin/perl
use strict;
use warnings;

use Parse::CPAN::Packages;
use WWW::Mechanize;

my $SEEN_FILE = "/home/rjbs/tmp/pep-mech-seen.txt";

my %seen_pkg = do {
  open my $seen_fh, "<", $SEEN_FILE or die "couldn't open seen file <: $!";
  map { chomp; $_ => 1 } <$seen_fh>;
};

my @dist_names = @ARGV;

my $p = Parse::CPAN::Packages->new(
  "/home/rjbs/minicpan/modules/02packages.details.txt.gz"
);

my @packages;

if (@dist_names) {
  @packages = map { $p->distribution($_)->packages } @package_names;
} else {
  @packages = grep {
    not(exists $seen_pkg{ $_->package })
    and
    $_->package =~ /^(?:Email|Mail|MIME|Net::(?:SMTP|POP|IMAP|QMTP|LMTP))/
  } $p->packages;
}

my $agent = WWW::Mechanize->new;

$agent->get('http://pep.pobox.com/wiki/');

$agent->follow_link(text_regex => qr/Log in/);

$agent->submit_form(
  form_name => 'userlogin',
  fields => {
    wpName      => "MechaRJBS",
    wpPassword  => 'cherrycokezero',
  }
);

open my $seen_fh, '>>', $SEEN_FILE or die "couldn't open seen file >>: $!";

for my $pkg (@packages) {
  my $pkg_name = $pkg->package;
  my $dist = $pkg->distribution->dist;

  $agent->get("http://pep.pobox.com/wiki/$pkg_name");

  if ($agent->content !~ /currently no text in this page/) {
    print "$pkg_name page exists already\n";
  } else {
    $agent->follow_link(text => 'Edit');

    $agent->submit_form(
      form_name => 'editform',
      fields => {
        wpTextbox1 => "{{module|module=$pkg_name|dist=$dist}}",
        wpSummary  => 'added stub',
      }
    );
    print "$pkg_name page created\n";
  }
  print {$seen_fh} "$pkg_name\n" or warn "couldn't append to seen file: $!";
}
