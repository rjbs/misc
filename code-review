#!/usr/bin/env perl
use 5.18.0;
use warnings;

use DateTime;
use Getopt::Long::Descriptive;
use HTTP::Tiny;
use MetaCPAN::API;
use Parse::CPAN::Packages::Fast;
use YAML::XS ();

my ($opt, $usage) = describe_options(
  '%c %o',
  [ 'rebuild'     => "rebuild the YAML file that holds state" ],
  [ 'project|p=s' => "project to mark reviewed"               ],
  [ 'message|m=s' => "commit message"                         ],
);

die "--message without --rebuild or --project is nonsensical"
  if $opt->message and not ($opt->rebuild or $opt->project);

my $mcpan = MetaCPAN::API->new;

my $saved = YAML::XS::LoadFile('code-review.yaml');

my $p = Parse::CPAN::Packages::Fast->new(
  "$ENV{HOME}/Dropbox/minicpan/modules/02packages.details.txt.gz"
);

my %is_tracked
  = map {; $_ => scalar(! $saved->{review} // '' eq 'never') } keys %$saved;

my $dist = {};

DIST: for my $d (
  sort { fc $a->dist cmp fc $b->dist } $p->latest_distributions
) {
  next unless $d->cpanid eq 'RJBS';
  next if $d->dist eq 'perl';

  $dist->{ $d->dist } = $d;
  $is_tracked{ $d->dist } = 1 unless exists $is_tracked{ $d->dist };
}

my @projects = sort {
  ($saved->{$a}{'last-review'} // '0') cmp ($saved->{$b}{'last-review'} // 0)
  ||
  fc $a cmp fc $b
} keys %is_tracked;

if (my $project = $opt->project) {
  die "project $project is not tracked\n" unless $is_tracked{ $project };
  $saved->{$project}{'last-review'}
    = DateTime->now->format_cldr('yyyy-MM-dd');
}

if ($opt->project or $opt->rebuild) {
  my %dump = map {; $_ => {
    ($saved->{$_}{home} ? (home => $saved->{$_}{home}) : ()),
    ($saved->{$_}{review} ? (review => $saved->{$_}{review}) : ()),
    ($saved->{$_}{'last-review'}
    ? ('last-review' => $saved->{$_}{'last-review'})
    : ()),
  } } @projects;

  YAML::XS::DumpFile('code-review.yaml', \%dump);

  system(qw(git add code-review.yaml)) and die "git-add failed\n";
  my $message = $opt->message
             || ($opt->project ? ("did review of " . $opt->project)
                               : "rebuilt code-review state file");

  system(qw(git add code-review.yaml)) and die "git-add failed\n";
  system(qw(git commit -m), $message)  and die "git-commit failed\n";
  exit;
}

for my $name (@projects) {
  my @notes = notes_for($name);
  say "=== $name ==========";
  printf "    %s\n", $_ for @notes;
  print  "    Everything is okay!  Who's have expected THAT?!\n" unless @notes;

  print "...hit enter...";
  scalar <STDIN>;
}

sub rt_data {
  state %rt_data;
  unless (%rt_data) {
    my $res = HTTP::Tiny->new->get(
      'https://rt.cpan.org/Public/bugs-per-dist.data-json'
    );
    die "Can't get RT bug count JSON" unless $res->{success};
    my ($json) = split /\n/, $res->{content};
    my $dists = JSON->new->decode($json);
    for my $dist (@$dists) {
      my $name = $dist->{dist};
      $rt_data{ $name } = {
        open    => 0,
        stalled => 0,
      };

      $rt_data{ $name }{open} += delete $dist->{counts}{$_}
        for qw(active patched new open);

      $rt_data{ $name }{active} += delete $dist->{counts}{$_}
        for qw(stalled);

      delete $dist->{counts}{$_} for qw(inactive resolved deleted rejected);

      if (my @keys = keys %{ $dist->{counts} }) {
        die "unknown statuses for tickets in $name: @keys\n";
      }
    }
  }

  return \%rt_data;
}

sub notes_for {
  my ($name) = @_;

  my $home = $saved->{$name}{home} // 'CPAN';

  if ($home ne 'CPAN') {
    return ("not hosted at CPAN, but at $home");
  }

  my $release = eval { $mcpan->release( distribution => $name ) };
  return ("couldn't find dist on metacpan") unless $release;

  my @notes;

  my $tracker = $release->{resources}{bugtracker};
  if (! $tracker->{web} or $tracker->{web} =~ /rt.cpan/) {
    push @notes, "still using rt.cpan.org";
  }

  my $lc;
  my $repo = $release->{resources}{repository}{url};
  if (! $repo) {
    push @notes, "no repository on file";
  } elsif ($repo !~ /github.com/) {
    push @notes, "not using GitHub for repo";
  } elsif ($repo =~ /\Q$name/i && $repo !~ /\Q$name/) {
    $lc = 1;
    push @notes, "GitHub repo is not capitalized correctly";
  }

  GH: {
    my $max = 100;
    my $reponame = $lc ? lc $name : $name;
    my $res = HTTP::Tiny->new->get(
      "https://api.github.com/repos/rjbs/$reponame/issues?per_page=$max"
    );
    unless ($res->{success}) {
      push @notes, "Could not get issues list from GitHub";
      last GH;
    }

    my $issues = JSON->new->decode($res->{content});
    last GH unless my $ct = @$issues;
    $ct = "$ct+" if $ct == $max;
    push @notes, "$ct GitHub issues";
  }

  my $rt_bugs = rt_data->{$name};
  for (qw(open stalled)) {
    push @notes, "rt.cpan.org tickets $_: $rt_bugs->{$_}"
      if $rt_bugs->{$_};
  }

  unless (($release->{metadata}{generated_by} // '') =~ /Dist::Zilla/) {
    push @notes, "dist not built with Dist::Zilla";
  }

  {
    my $res = HTTP::Tiny->new->get(
      "http://cpants.cpanauthors.org/dist/$name.json",
    );

    if ($res->{success}) {
      my $data = JSON->new->decode($res->{content});
      for my $result (@{ $data->{kwalitee}[0] }) {
        next if $result->{value};
        next if $result->{is_experimental};
        next if $result->{is_extra};
        push @notes, "kwalitee test failed: $result->{name}";
      }
    } else {
      push @notes, "could not get CPANTS results";
    }
  }

  return @notes;
}
