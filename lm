#!/usr/bin/env perl
use strict;
use warnings;
use File::Find::Rule;
use List::Util 'max';
use Term::ANSIColor;
use Time::Duration;

my $ROOT = "$ENV{HOME}/Maildir";

my @dirs = File::Find::Rule->directory
                           ->maxdepth(1)
                           ->exec(sub { -d "$_/new" })
                           ->in($ROOT);

my $total = 0;
for my $dir (sort @dirs) {
  next if $dir =~ /spam.\d{4}/;
  my @files = File::Find::Rule->file
                              ->maxdepth(2)
                              ->exec(sub { $_[2] =~ m{/(?:new|cur)/} })
                              ->not(File::Find::Rule->name(qr/,\w*S\w*\z/))
                              ->in($dir);

  next unless @files;

  my $latest = $^T - max map {; (stat)[9] } @files;

  $total += @files;
  $dir =~ s{\Q$ROOT\E/?}{/INBOX};
  $dir =~ tr|.|/|;
  printf "%-30s : %4u : %s%3s%s\n",
    $dir,
    0+@files,
    color( $latest <  900 ? 'bold green'
         : $latest < 3600 ? 'bold yellow'
         :                  'reset'),
    concise(duration($latest, 1)),
    color('reset');
}

$total ? (printf "%30s : %4u\n", 'total', $total) : (print "no new mail\n");
